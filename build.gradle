// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.versionName = '2.3.0'
    ext.groupName = 'com.chibatching.kotpref'

    ext.kotlinVersion = '1.2.10'
    ext.junitVersion = '4.12'
    ext.robolectricVersion = '3.4.2'
    ext.assertjVersion = '3.8.0'
    ext.testingVersion = '0.1'

    ext.sdkVersion = 26
    ext.buildToolsVersion = '26.0.2'
    ext.minSdkVersion = 9

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.15'
    }
}

allprojects {
    apply plugin: 'jacoco'
    repositories {
        jcenter()
        google()
    }
    jacoco {
        toolVersion = "0.7.9"
    }
}

configure(subprojects.findAll {it.name != 'sample'}) {
    task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

        reports {
            xml.enabled = true
            html.enabled = true
        }

        def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*']
        def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
        def mainSrc = "${project.projectDir}/src/main/kotlin"

        sourceDirectories = files([mainSrc])
        classDirectories = files([debugTree], [kotlinDebugTree])
        executionData = fileTree(dir: "$buildDir", includes: [
                "jacoco/testDebugUnitTest.exec"
        ])
    }
}

static def env(String name, String defValue) {
    def value = System.getenv()[name]
    return value == null ? defValue : value
}
